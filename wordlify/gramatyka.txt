<program> ::= <opt_wss_with_opt_nls> <instrs> <opt_wss_with_opt_nls>

<instrs> ::= <instrs> <opt_wss_with_nls> <instrs_line> | <instrs_line>

<instrs_line> ::= <instrs_line_without_last_instr> <opt_wss> <instr_with_opt_sc> | <instr_with_opt_sc>
<instrs_line_without_last_instr> ::= <instrs_line_without_last_instr> <opt_wss> <instr_with_sc> | <instr_with_sc>

<instr_with_opt_sc> ::= <instr_with_sc> | <instr>
<instr_with_sc> ::= <instr> <opt_wss> ;
<instr> ::= <rename> | <remove> | <move> | <copy> | <download> | <write> | <wait> | <execute> | <exit>

<rename> ::= rename <wss> <str_or_id> <wss> <str_or_id>
<remove> ::= remove <wss> <str_or_id>
<move> ::= move <wss> <str_or_id> <wss> <str_or_id>
<copy> ::= copy <wss> <str_or_id> <wss> <str_or_id>
<download> ::= download <wss> <str_or_id> <wss> <str_or_id>
<write> ::= write <wss> <str_or_id> <wss> <str_or_id>
<wait> ::= wait <wss> <nneg_num_or_id>
<execute> ::= execute <wss_str_or_id_repeated>
<exit> ::= exit <wss> <nneg_int> | exit <wss> <id>

<nneg_num_or_id> ::= <nneg_int> | <nneg_float> | <id>

<nneg_float> ::= <nneg_int> . <digits>
<nneg_int> ::= [1-9] <opt_digits> | 0
<digits> ::= <digits> [0-9] | [0-9]
<opt_digits> ::= <opt_digits> [0-9] | ""

<wss_str_or_id_repeated> ::= <wss_str_or_id_repeated> <wss> <str_or_id> | <wss> <str_or_id>
<str_or_id> ::= <str> | <id>
<str> ::= " <str_chars> "
<id> ::= <letter> <id_chars> | <letter>

<id_chars> ::= <id_chars> <id_char> | <id_char>
<id_char> ::= <letter> | <digit> | _

<str_chars> ::= <str_chars> <str_char> | <str_char> | ""
<str_char> ::= <letter> | <digit> | <other_char> | \" | \\ | \n | \r

<opt_wss_with_opt_nls> ::= <opt_wss_with_opt_nls> <nl_or_ws_or_none> | <nl_or_ws_or_none>
<opt_wss_with_nls> ::= <opt_wss_with_nls> <opt_wss_with_nl> | <opt_wss_with_nl>

<opt_wss_with_nl> ::= <opt_wss> <nl> <opt_wss>

<nl_or_ws_or_none> ::= <nl> | <ws> | ""
<nl> ::= # <chars> "\n" | "\n" 

<opt_wss> ::= <wss> | ""
<wss> ::= <wss> <ws> | <ws>

<ws> ::= " " | "\t"
<opt_sc> ::= ; | ""

<chars> ::= <chars> <char> | ""
<char> ::= <letter> | <digit> | <other_char> | " | \

<letter> ::= [A-Z] | [a-z]
<digit> ::= [0-9]
<other_char> ::= ! | # | $ | % | & | ' | ( | ) | * | + | , | - | . | / | : | ; | < | = | > | ? | @ | [ | ] | ^ | _ | ` | { | "|" | } | ~ | " " | \t
