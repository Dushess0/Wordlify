<program> ::= <opt_wss_with_opt_nls> <instrs> <opt_wss_with_opt_nls>

<instrs> ::= <instrs> <opt_wss_with_nls> <instrs_line> | <instrs_line>

<instrs_line> ::= <instrs_line_without_last_instr> <opt_wss> <instr_with_opt_sc> | <instr_with_opt_sc>
<instrs_line_without_last_instr> ::= <instrs_line_without_last_instr> <opt_wss> <instr_with_sc> | <instr_with_sc>

<instr_with_opt_sc> ::= <instr_with_sc> | <instr>
<instr_with_sc> ::= <instr> <opt_wss> ;
<instr> ::= <print> | <rename> | <remove> | <move> | <copy> | <download> | <write> | <wait> | <execute> | <exit> | <assign_begin> <value> 

<assign_begin> ::= <id> <opt_wss> = <opt_wss> 
<value> ::= <return_fn> | <str> 

<return_fn> ::= <exist> | <read> | <time> | <get_files> | <date_modified> | <size> | <file> | <folder> | <args> 

<exist> ::= exist <one_str_arg> 
<print> ::= print <one_str_arg> 
<rename> ::= rename <two_str_args> 
<remove> ::= remove <one_str_arg> 
<move> ::= move <two_str_args> 
<copy> ::= copy <two_str_args> 
<download> ::= download <two_str_args> 
<read> ::= read <one_str_arg> 
<write> ::= write <two_str_args> 
<wait> ::= wait <opt_wss_with_opt_nls> ( <opt_wss_with_opt_nls> <nneg_num_or_id> <opt_wss_with_opt_nls> ) 
<execute> ::= execute <opt_wss_with_opt_nls> ( <opt_wss_with_opt_nls> <str_args> <opt_wss_with_opt_nls> ) 
<time> ::= time 
<get_files> ::= getFiles <one_str_arg> 
<date_modified> ::= dateModified <one_str_arg> 
<size> ::= size <one_str_arg> 
<file> ::= file 
<folder> ::= folder 
<args> ::= args 
<exit> ::= exit <opt_wss_with_opt_nls> ( <opt_wss_with_opt_nls> <nneg_int_or_id> <opt_wss_with_opt_nls> ) 

<one_str_arg> ::= <opt_wss_with_opt_nls> ( <opt_wss_with_opt_nls> <str_or_id> <opt_wss_with_opt_nls> ) 
<two_str_args> ::= <opt_wss_with_opt_nls> ( <opt_wss_with_opt_nls> <str_or_id> <opt_wss_with_opt_nls> , <opt_wss_with_opt_nls> <str_or_id> <opt_wss_with_opt_nls> ) 
<str_args> ::= <str_or_id> <next_str_args> 
<next_str_args> ::= <next_str_args> <opt_wss_with_opt_nls> , <opt_wss_with_opt_nls> <str_or_id> | "" 

<nneg_int_or_id> ::= <nneg_int> | <id> 
<nneg_num_or_id> ::= <nneg_int> | <nneg_float> | <id>

<nneg_float> ::= <nneg_int> . <digits>
<nneg_int> ::= [1-9] <opt_digits> | 0
<digits> ::= <digits> [0-9] | [0-9]
<opt_digits> ::= <opt_digits> [0-9] | ""

<wss_str_or_id_repeated> ::= <wss_str_or_id_repeated> <wss> <str_or_id> | <wss> <str_or_id>
<str_or_id> ::= <str> | <id>
<str> ::= " <str_chars> "
<id> ::= <letter> <id_chars> | <letter>

<id_chars> ::= <id_chars> <id_char> | <id_char>
<id_char> ::= <letter> | <digit> | _

<str_chars> ::= <str_chars> <str_char> | <str_char> | ""
<str_char> ::= <letter> | <digit> | <other_char> | \" | \\ | \n | \r

<opt_wss_with_opt_nls> ::= <opt_wss_with_opt_nls> <nl_or_ws_or_none> | <nl_or_ws_or_none>
<opt_wss_with_nls> ::= <opt_wss_with_nls> <opt_wss_with_nl> | <opt_wss_with_nl>

<opt_wss_with_nl> ::= <opt_wss> <nl> <opt_wss>

<nl_or_ws_or_none> ::= <nl> | <ws> | ""
<nl> ::= # <opt_chars> "\n" | "\n" 

<opt_wss> ::= <wss> | ""
<wss> ::= <wss> <ws> | <ws>

<ws> ::= " " | \t
<opt_sc> ::= ; | ""

<opt_chars> ::= <opt_chars> <char> | ""
<char> ::= <letter> | <digit> | <other_char> | " | \

<letter> ::= [A-Z] | [a-z]
<digit> ::= [0-9]
<other_char> ::= ! | # | $ | % | & | ' | ( | ) | * | + | , | - | . | / | : | ; | < | = | > | ? | @ | [ | ] | ^ | _ | ` | { | "|" | } | ~ | " " | \t
